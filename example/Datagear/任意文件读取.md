# Datagear漏洞与ql实现

>学习ql的过程无意间发现了一个项目https://github.com/datageartech/datagear 感觉可能存在比较简单的漏洞，于是想挖掘一下并且使用ql去实现。之前是[safe6sec](https://github.com/safe6Sec)师傅带着完成了一个非常感谢。https://github.com/Firebasky/CodeqlLearn/blob/main/example/Sentinel/ssrf.md

## 任意文件读取

![image-20220107210234891](https://user-images.githubusercontent.com/63966847/148550868-25b09ed1-a3f9-4cd9-b473-bdd014450bd0.png)


演示

![image-20220107210250612](https://user-images.githubusercontent.com/63966847/148550881-98915067-f875-44a3-92fd-3f31450045d5.png)



## CodeQL实现

简单的看完了项目，让我们通过ql来实现吧（其实ql才是主角.



我们的source点可以先使用默认的(**RemoteFlowSource**)里面包含了springboot框架的一些source点

```java
override predicate isSource(DataFlow::Node src) { 
    src instanceof RemoteFlowSource//默认的
   }
```

而sink点就是

![image-20220107210441904](https://user-images.githubusercontent.com/63966847/148550895-f8bc4a82-7693-4a44-b53c-7b1b8be27bbb.png)


就是write方法污染点是第一个参数，也就是说只要污染点能够传递到write方法就是完整的漏洞。

```java
//sink  IOUtil.write(tempFile, out);
	override predicate isSink(DataFlow::Node sink) {
    exists(Method method, MethodAccess call |
      method.hasName("write")
      and
      call.getMethod() = method 
      and
      sink.asExpr() = call.getArgument(0)
    )
  }
```

完整的

```java
/**
 * @id datagear
 * @name Readfile
 * @description Arbitrary file read
 * @kind path-problem
 * @precision high
 */
import java
import semmle.code.java.dataflow.TaintTracking
import semmle.code.java.dataflow.FlowSources
import DataFlow::PathGraph

class ReadfileConfig extends TaintTracking::Configuration {
  ReadfileConfig() { this = "ReadfileConfig" }

  override predicate isSource(DataFlow::Node src) { 
    src instanceof RemoteFlowSource//默认的
   }

   //sink  IOUtil.write(tempFile, out);
	override predicate isSink(DataFlow::Node sink) {
    exists(Method method, MethodAccess call |
      method.hasName("write")
      and
      call.getMethod() = method 
      and
      sink.asExpr() = call.getArgument(0)
    )
  }
}

from ReadfileConfig config, DataFlow::PathNode source, DataFlow::PathNode sink
where config.hasFlowPath(source, sink)
select source.getNode(), source, sink, "source"
```

可以看到确实跑出了漏洞，不过163个结果太多，肯定有误报的。

![image-20220107210551312](https://user-images.githubusercontent.com/63966847/148550908-e1fe29c8-e42d-45b3-93f7-924b6b1cfb2c.png)



我们来完善一下，简单的看了一下sink点有时候并没有调用org.datagear.util.IOUtil#write所以我们加上他。

```java
override predicate isSink(DataFlow::Node sink) {
    exists(Method method, MethodAccess call |
      method.hasName("write")
      and
      call.getMethod() = method 
      and  
      method.getDeclaringType().getAnAncestor().hasQualifiedName("org.datagear.util", "IOUtil")
      and
      sink.asExpr() = call.getArgument(0)
    )
  }
```

这下子跑出来54个结果还可以。不过里面的误报还是比较多。类似于 **getOriginalFilename()**方法是防止了目录穿越的所以我们需要给他clear。

就使用 **isSanitizer**方法

```java
//解决误报,调用getOriginalFilename方法的给他clear
override predicate isSanitizer(DataFlow::Node node) {
    exists(MethodAccess call ,Method method|
           method.hasName("getOriginalFilename")
           and
           call.getMethod() = method
           and
           call.getAChildExpr()=node.asExpr()
          )
}
```

现在看看结果是38个，我们继续。简单的看看结果发现有些不是路由传递过来的也当成看source导致误报又添加了，使用我们自己写一个source点。

```java
class ControllerAnno extends Annotation {
  ControllerAnno() {
      this.getType().hasQualifiedName("org.springframework.web.bind.annotation", "RequestMapping") or
      this.getType().hasQualifiedName("org.springframework.web.bind.annotation", "GetMapping")
  }
}
private class SpringBootSource extends RemoteFlowSource {
  SpringBootSource(){
      this.asParameter().getCallable().getAnAnnotation() instanceof ControllerAnno
  }
  override string getSourceType() { result = "SpringBoot input parameter" }
}
```

nice，又过滤了一些，现在有27个结果。在继续看结果发现有的结果是触发了 **IOUtil.write(in, Out);**因为不同的write方法，我们需要的是

```javascript
public static void write(File file, OutputStream out)
```

![image-20220107210821582](https://user-images.githubusercontent.com/63966847/148550928-72b4ec8c-7f20-4d44-bb18-9248143489d5.png)


而其他的基本上是写文件操作，现在我们讨论的是任意读文件所以我们先过滤他。在一次修改**isSink**

```java
//sink  IOUtil.write(tempFile, out);
override predicate isSink(DataFlow::Node sink) {
    exists(Method method, MethodAccess call |
           method.hasName("write")
           and
           call.getMethod() = method 
           and  
           method.getDeclaringType().getAnAncestor().hasQualifiedName("org.datagear.util", "IOUtil")
           and 
           call.getArgument(0).getType().hasName("File")//选择唯一的write方法
           and
           sink.asExpr() = call.getArgument(0)
          )
}
```

现在跑一下结果有9个,并且存在漏洞.

![image-20220107210935493](https://user-images.githubusercontent.com/63966847/148550942-f916780f-24c8-4015-b12c-eaf6494e7d36.png)


完整ql

```java
/**
 * @id datagear
 * @name Readfile
 * @description Arbitrary file read
 * @kind path-problem
 * @precision high
 */
import java
import semmle.code.java.dataflow.TaintTracking
import semmle.code.java.dataflow.FlowSources
import DataFlow::PathGraph

class ControllerAnno extends Annotation {
  ControllerAnno() {
      this.getType().hasQualifiedName("org.springframework.web.bind.annotation", "RequestMapping") or
      this.getType().hasQualifiedName("org.springframework.web.bind.annotation", "GetMapping")
  }
}
private class SpringBootSource extends RemoteFlowSource {
  SpringBootSource(){
      this.asParameter().getCallable().getAnAnnotation() instanceof ControllerAnno
  }
  override string getSourceType() { result = "SpringBoot input parameter" }
}


class ReadfileConfig extends TaintTracking::Configuration {
  ReadfileConfig() { this = "ReadfileConfig" }

  override predicate isSource(DataFlow::Node src) { 
    src instanceof SpringBootSource//自己实现的
   }

   //sink  IOUtil.write(tempFile, out);
   override predicate isSink(DataFlow::Node sink) {
    exists(Method method, MethodAccess call |
      method.hasName("write")
      and
      call.getMethod() = method 
      and  
      method.getDeclaringType().getAnAncestor().hasQualifiedName("org.datagear.util", "IOUtil")
      and 
      call.getArgument(0).getType().hasName("File")//选择唯一的write方法
      and
      sink.asExpr() = call.getArgument(0)
    )
  }

  //解决误报,调用getOriginalFilename方法的给他clear
  override predicate isSanitizer(DataFlow::Node node) {
    exists(MethodAccess call ,Method method|
      method.hasName("getOriginalFilename")
      and
      call.getMethod() = method
      and
      call.getAChildExpr()=node.asExpr()
    )
  }
}

from ReadfileConfig config, DataFlow::PathNode source, DataFlow::PathNode sink
where config.hasFlowPath(source, sink)
select source.getNode(), source, sink, "source"
```

